import os

#### Initialize input ####

# Get job prefix and confirm config exists
try:
    JOB_PREFIX = config["job_prefix"]
except KeyError as e:
    print("Error finding a job prefix. Did you use a --configfile?")
    raise e
RESULT_DIR = f"results/{JOB_PREFIX}"

# Gather input files
DATA_DIR = os.path.expanduser(config["data_dir"]) # expanduser allows ~
DATA_EXT = config["data_ext"]

# antismash results as input
if not DATA_EXT:
    GENOMES, toss = glob_wildcards(f"{DATA_DIR}/{{genome}}/{{toss}}.json")
    AS_DIR = DATA_DIR
    print(f"{len(GENOMES)} antiSMASH results found")

# genome files as input
else:
    GENOMES, = glob_wildcards(f"{DATA_DIR}/{{genome}}.{DATA_EXT}")
    print(f"{len(GENOMES)} {DATA_EXT} files found")
    AS_DIR = f"{RESULT_DIR}/antismash"
if not GENOMES:
    raise OSError(f"Error! Unable to find data in '{DATA_DIR}' "
                  f"with extension '{DATA_EXT}'.")

## Result locations
BIG_DIR = f"{RESULT_DIR}/bigscape"


rule all:
    input:
        # tabulate_regions
        f"{RESULT_DIR}/all_regions.tsv",
        # count_regions
        f"{RESULT_DIR}/region_counts.tsv",
        # bigscape
        f"{BIG_DIR}.tar.gz"


#### AntiSMASH rules ####

# TODO: give logging on failed runs - check if a dir is empty after this
# TODO: gff support
rule run_antismash:
    input:
        genomes = f"{DATA_DIR}/{{GENOMES}}.{DATA_EXT}",
    params:
        antismash = config["antismash_command"],
        antismash_flags = config["antismash_flags"],
    output: directory(f"{AS_DIR}/{{GENOMES}}")
    conda: config["antismash_conda"]
    shell: "{params.antismash} {input.genomes} --output-dir {output} "
                "-c 1 {params.antismash_flags} || mkdir -p {output}"

rule tabulate_regions:
    input: expand(f"{AS_DIR}/{{genomes}}", genomes = GENOMES)
    params: AS_DIR
    output: f"{RESULT_DIR}/all_regions.tsv"
    shell: "python workflow/scripts/tabulate_regions.py {params} {output}"

rule count_regions:
    input: expand(f"{AS_DIR}/{{genomes}}", genomes = GENOMES)
    params:
        asdir = AS_DIR,
        contig = "--contig" if config["contig"] else "",
        hybrid= "--hybrid" if config["hybrid"] else ""
    output: f"{RESULT_DIR}/region_counts.tsv"
    shell: "python workflow/scripts/count_regions.py {params.asdir} {output} {params.contig} {params.hybrid}"


## BiG-SCAPE rules

rule run_bigscape:
    input: expand(f"{AS_DIR}/{{genomes}}", genomes = GENOMES)
    params:
        asdir = AS_DIR,
        outdir = BIG_DIR,
        bigscape_command = config["bigscape_command"],
        bigscape_flags = config['bigscape_flags']
    output: f"{BIG_DIR}/index.html"
    conda: config["bigscape_conda"]
    shell: "{params.bigscape_command} -i {params.asdir} -o {params.outdir} {params.bigscape_flags}"

rule tar_bigscape:
    input: f"{BIG_DIR}/index.html"
    params: BIG_DIR
    output: f"{BIG_DIR}.tar.gz"
    shell: "tar --exclude 'cache' -zcf {output} -C {params} ."
